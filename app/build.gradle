apply plugin: 'com.android.application'

// Apply the Google Services plugin (if it's not there already).
apply plugin: 'com.google.gms.google-services'

// Add the Firebase Crashlytics plugin.
apply plugin: 'com.google.firebase.crashlytics'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'realm-android'

// Apply the Performance Monitoring plugin
apply plugin: 'com.google.firebase.firebase-perf'


gradle.taskGraph.whenReady { taskGraph ->
    def buildIncreaseOn = ["assembleDevDebug",
                           "assembleDevRelease",
                           "assemblePlayStoreDebug",
                           "assemblePlayStoreRelease"]

    println("Tasks:")
    taskGraph.allTasks.forEach { task ->
        println(task.name)

        if (task.name in buildIncreaseOn) {
            println("Increasing build number")
            android.autoIncrementBuildNumber()
        }
    }
}

/* ==================== VERSIONING ============================= */
def versionPropsFile = file('version.properties')
def versionBuild
def versionDate = new Date().format("dd MMM yyyy | HH:mm:ss z")

Properties versionProps = new Properties()
versionProps.load(new FileInputStream(versionPropsFile))
/* ==============================================================*/

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.0"

    versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1

    ext.autoIncrementBuildNumber = {
        versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
        versionProps['VERSION_BUILD'] = versionBuild.toString()
        versionProps['VERSION_DATE'] = new Date().format("dd MMM yyyy | HH:mm:ss z")
        versionProps.store(versionPropsFile.newWriter(), null)
    }

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        applicationId "com.joeshuff.dddungeongenerator"
        minSdkVersion 18
        targetSdkVersion 30
        versionCode 21
        versionName "2.4"
        buildConfigField 'String', 'buildNumber', '"' + String.valueOf(versionBuild) + '"'
        buildConfigField 'String', 'buildTime', '"' + String.valueOf(versionDate) + '"'

        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "user"

    productFlavors {
        dev {
            dimension "user"
            applicationIdSuffix ".dev"
            buildConfigField 'boolean', 'DEV', 'true'
        }
        beta {
            dimension "user"
            versionNameSuffix ".beta-" + String.valueOf(versionBuild)
            buildConfigField 'boolean', 'DEV', 'false'
        }
        playStore {
            dimension "user"
            buildConfigField 'boolean', 'DEV', 'false'
        }
    }

    packagingOptions {
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.2.0-alpha02'

    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'androidx.appcompat:appcompat:1.0.0-beta01'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.2.0-alpha03'
    implementation 'androidx.palette:palette:1.0.0'

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.10'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0-beta.1'

    implementation 'com.otaliastudios:zoomlayout:1.6.0'
    implementation 'com.github.warkiz.tickseekbar:tickseekbar:0.1.4'

    implementation 'com.github.joeShuff:EmptyRecyclerView:1.0.4'

    implementation 'androidx.browser:browser:1.0.0'

    implementation 'com.github.barteksc:android-pdf-viewer:3.2.0-beta.1'

    implementation 'net.sourceforge.streamsupport:streamsupport:1.7.0'

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Add the Firebase Crashlytics SDK.
    implementation 'com.google.firebase:firebase-crashlytics:17.2.2'

    // Recommended: Add the Google Analytics SDK.
    implementation 'com.google.firebase:firebase-analytics:18.0.0'

    // Add the dependency for the Performance Monitoring library
    implementation 'com.google.firebase:firebase-perf:19.0.5'
}

apply plugin: 'com.google.gms.google-services'
